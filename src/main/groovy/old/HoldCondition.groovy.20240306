import groovy.transform.ToString
import org.oopscraft.fintics.calculator.*
import org.oopscraft.fintics.model.Ohlcv
import org.oopscraft.fintics.trade.Tool


interface Scorable {
    def getAverage()
}

@ToString(includeNames = true)
class Score extends LinkedHashMap<String, BigDecimal> implements Scorable {
    def getAverage() {
        return this.values().empty ? 0 : this.values().average()
    }
}

class ScoreGroup extends LinkedHashMap<String, Scorable> implements Scorable {
    def getAverage() {
        return this.values().collect{it.getAverage()}.average()
    }
    @Override
    String toString() {
        return this.join('\n')
    }
}

interface Analyzable {
    Scorable getMomentumScore()
    Scorable getOversoldScore()
    Scorable getOverboughtScore()
}

class Analysis implements Analyzable {
    List<Ohlcv> ohlcvs
    List<Ema> fastEmas
    List<Ema> middleEmas
    List<Ema> slowEmas
    List<BollingerBand> bollingerBands
    List<Macd> macds
    List<Rsi> rsis
    List<Dmi> dmis
    List<Obv> obvs
    List<ChaikinOscillator> chaikinOscillator

    Analysis(List<Ohlcv> ohlcvs) {
        this.ohlcvs = ohlcvs
        this.fastEmas = Tool.calculate(ohlcvs, EmaContext.of(10))
        this.middleEmas = Tool.calculate(ohlcvs, EmaContext.of(20))
        this.slowEmas = Tool.calculate(ohlcvs, EmaContext.of(60))
        this.bollingerBands = Tool.calculate(ohlcvs, BollingerBandContext.DEFAULT)
        this.macds = Tool.calculate(ohlcvs, MacdContext.DEFAULT)
        this.rsis = Tool.calculate(ohlcvs, RsiContext.DEFAULT)
        this.dmis = Tool.calculate(ohlcvs, DmiContext.DEFAULT)
        this.obvs = Tool.calculate(ohlcvs, ObvContext.DEFAULT)
        this.chaikinOscillator = Tool.calculate(ohlcvs, ChaikinOscillatorContext.DEFAULT)
    }

    @Override
    Scorable getMomentumScore() {
        def score = new Score()
        // define
        def ohlcv = this.ohlcvs.first()
        // ema
        def fastEma = this.fastEmas.first()
        def slowEma = this.middleEmas.first()
        score.emaValueFastOverSlow = fastEma.value > slowEma.value ? 100 : 0
        // bollinger band
        def bollingerBand = this.bollingerBands.first()
        score.bollingerBandPriceOverMiddle = ohlcv.closePrice > bollingerBand.middle ? 100 : 0
        // macd
        def macd = this.macds.first()
        score.macdValue = macd.value > 0 ? 100 : 0
        score.macdValueOverSignal = macd.value > 0 && macd.value > macd.signal ? 100 : 0
        score.macdOscillator = macd.oscillator > 0 ? 100 : 0
        // rsi
        def rsi = this.rsis.first()
        score.rsiValue = rsi.value > 50 ? 100 : 0
        score.rsiValueOverSignal = rsi.value > 50 && rsi.value > rsi.signal ? 100 : 0
        // dmi
        def dmi = this.dmis.first()
        score.dmiPdiOverMdi = dmi.pdi > dmi.mdi ? 100 : 0
        score.dmiAdx = dmi.adx > 25 && dmi.pdi > dmi.mdi ? 100 : 0
        // obv
        def obv = this.obvs.first()
        score.obvValueOverSignal = obv.value > obv.signal ? 100 : 0
        // co
        def chaikinOscillator = this.chaikinOscillator.first()
        score.coValue = chaikinOscillator.value > 0 ? 100 : 0
        score.coValueOverSignal = chaikinOscillator.value > chaikinOscillator.signal ? 100 : 0
        // return
        return score
    }

    @Override
    Scorable getOversoldScore() {
        def score = new Score()
        // rsi
        def rsi = this.rsis.first()
//        score.rsiValue = rsi.value <= 30 ? 100 : 0
//        score.rsiValueOverSignal  = rsi.value < 40 && rsi.value > rsi.signal ? 100 : 0
        // bollinger band
        def bollingerBand = this.bollingerBands.first()
        if (bollingerBand.bandWidth > 0.03) {
            score.bollingerBand = 100
        }
//        score.bollingerBandPercentB = bollingerBand.percentB <= 0 ? 100 : 0
        // return
        return score
    }

    @Override
    Scorable getOverboughtScore() {
        def score = new Score()
        // rsi
        def rsi = this.rsis.first()
        //score.rsiValue = rsi.value >= 70 ? 100 : 0
//        score.rsiValueOverSignal  = rsi.value > 60 && rsi.value < rsi.signal ? 100 : 0
        // bollinger band
        def bollingerBand = this.bollingerBands.first()
        if (bollingerBand.bandWidth > 0.03) {
            score.bollingerBand = 100
        }
        // return
        return score
    }


    @Override
    String toString() {
        return [momentumScore: "${this.getMomentumScore().getAverage()}",
                overboughtScore: "${this.getOverboughtScore().getAverage()}",
                oversoldScore: "${this.getOversoldScore().getAverage()}"
        ].toString()
    }
}

class AnalysisGroup extends LinkedHashMap<String,Analysis> implements Analyzable {

    AnalysisGroup(Map map) {
        super(map)
    }

    @Override
    Scorable getMomentumScore() {
        def scoreGroup = new ScoreGroup()
        this.each{it -> scoreGroup.put(it.key, it.value.getMomentumScore())}
        return scoreGroup
    }

    @Override
    Scorable getOversoldScore() {
        def scoreGroup = new ScoreGroup()
        this.each{it -> scoreGroup.put(it.key, it.value.getOversoldScore())}
        return scoreGroup
    }

    @Override
    Scorable getOverboughtScore() {
        def scoreGroup = new ScoreGroup()
        this.each{it -> scoreGroup.put(it.key, it.value.getOverboughtScore())}
        return scoreGroup
    }

    @Override
    String toString() {
        return [momentumScore: "${this.getMomentumScore().getAverage()}",
                overboughtScore: "${this.getOverboughtScore().getAverage()}",
                oversoldScore: "${this.getOversoldScore().getAverage()}"
        ].toString() + super.toString()
    }
}

//==========================
// defines
//==========================
def hold = null
def assetId = assetIndicator.getAssetId()
def assetName = "${assetIndicator.getAssetName()}(${assetId})"

// default
List<Ohlcv> ohlcvs = assetIndicator.getOhlcvs(Ohlcv.Type.MINUTE, 1)
def priceZScore = Tool.zScore(ohlcvs.take(20).collect{it.closePrice})
if (priceZScore.abs() < 1.0) {
    log.info("[{}] skip - price z-score is under 1.0", assetName)
    return null
}
def analysis = new Analysis(ohlcvs)

// daily
List<Ohlcv> dailyOhlcvs = assetIndicator.getOhlcvs(Ohlcv.Type.DAILY, 1)
def dailyAnalysis = new Analysis(dailyOhlcvs)

// wave
def waveAnalysis = new AnalysisGroup(
        minute5: new Analysis(assetIndicator.getOhlcvs(Ohlcv.Type.MINUTE, 5))
)

// tide
def tideAnalysis = new AnalysisGroup(
        hourly: new Analysis(assetIndicator.getOhlcvs(Ohlcv.Type.MINUTE, 60)),
        daily: new Analysis(assetIndicator.getOhlcvs(Ohlcv.Type.DAILY, 1))
)

//=============================
// additional
//=============================
// price change percentage
def pricePctChange = Tool.pctChange([
        ohlcvs.first().closePrice,
        dailyOhlcvs.first().openPrice
])
// high price change average
def highPricePctChangeAverage = dailyOhlcvs.take(14)
        .collect{(it.highPrice-it.openPrice)/it.closePrice*100}
        .average()
// low price change average
def lowPricePctChangeAverage = dailyOhlcvs.take(14)
        .collect{(it.lowPrice-it.openPrice)/it.closePrice*100}
        .average()

//=============================
// logging
//=============================
log.info("[{}] analysis: {}", assetName, analysis)
log.info("[{}] dailyAnalysis: {}", assetName, dailyAnalysis)
log.info("[{}] waveAnalysis: {}", assetName, waveAnalysis)
log.info("[{}] tideAnalysis: {}", assetName, tideAnalysis)
log.info("[{}] pricePctChange: {}", assetName, pricePctChange)
log.info("[{}] highPricePctChangeAverage: {}", assetName, highPricePctChangeAverage)
log.info("[{}] lowPricePctChangeAverage: {}", assetName, lowPricePctChangeAverage)

//=============================
// trade
//=============================

log.info("################ {}", waveAnalysis.minute5.bollingerBands.first())
// buy
if (analysis.getMomentumScore().getAverage() >= 70) {
    // oversold
    if (waveAnalysis.getOversoldScore().getAverage() >= 50) {
        hold = 1
    }
    // alt-1. 금일 하락 분이 최근 평균 이상 이면 매수
//    if (pricePctChange < lowPricePctChangeAverage) {
//        hold = 1
//    }
}

// sell
if (analysis.getMomentumScore().getAverage() <= 30) {
    // overbought
    if (waveAnalysis.getOverboughtScore().getAverage() >= 50) {
        hold = 0
    }
    // alt-1. 금일 상승 분이 최근 평균 이상 이면 매도
//    if (pricePctChange > highPricePctChangeAverage) {
//        hold = 0
//    }
}

//==============================
// default fallback
//==============================
if (tideAnalysis.getMomentumScore().getAverage() <= 30) {
    hold = 0
}

//==============================
// return
//==============================
return hold

